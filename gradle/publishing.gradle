/*
 * Copyright 2021 Grabtaxi Holdings PTE LTE (GRAB)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "org.jetbrains.dokka"

task sourcesJar(type: Jar) {
    group = "publishing"
    archiveClassifier.set("sources")
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    group = "publishing"
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            // Gradle plugin marker if it exists
            named("grazelPluginPluginMarkerMaven") { publication ->
                configurePom(publication)
            }

            release(MavenPublication) { publication ->
                groupId project.findProperty("groupId")
                artifactId project.name
                version project.findProperty("versionName")

                from components.java

                artifact sourcesJar
                artifact javadocJar

                configurePom(publication)
            }
        }
    }
}

private void configurePom(MavenPublication publication) {
    publication.pom {
        name = project.name
        description = project.description
        url = website
        licenses {
            license {
                name = "Apache License, Version 2.0"
                url = "https://raw.githubusercontent.com/grab/Grazel/master/LICENSE.md"
            }
        }
        developers {
            developer {
                id = "arunkumar9t2"
                name = "Arunkumar"
                email = "hi@arunkumar.dev"
            }
            developer {
                id = "roscrazy"
                name = "Minh Nguyen"
                email = "minhnguyen.gtvt@gmail.com"
            }
            developer {
                id = "MarinaShaposhnikova"
                name = "Maryna Shaposhnikova"
                email = "marina95meier@gmail.com"
            }
        }
        scm {
            connection = "git@github.com:grab/Grazel.git"
            developerConnection = "git@github.com:grab/Grazel.git"
            url = website
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]
signing {
    sign publishing.publications
}